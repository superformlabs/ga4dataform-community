/*
	This file is part of "GA4 Dataform Package".
	Copyright (C) 2023-2026 Superform Labs <support@superformlabs.eu>
	Artem Korneev, Jules Stuifbergen,
	Johan van de Werken, Kriszti√°n Korpa,
	Simon Breton

	This program is free software: you can redistribute it and/or modify
	it under the terms of the GNU General Public License as published by
	the Free Software Foundation, version 3 of the License.

	This program is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU General Public License in the LICENSE.txt file for more details.

	You should have received a copy of the GNU General Public License
	along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/


config {
    type: "table",
    schema: dataform.projectConfig.vars.OUTPUTS_DATASET,
    description: "Daily session aggregate table - can be used for LP report, marketing, etc",
    bigquery: {
        partitionBy: "session_date",
        labels: require("includes/core/helpers.js").helpers.storageLabels()
    },
}

js {
  const { helpers } = require("includes/core/helpers");
}

pre_operations {
  set @@query_label = "${helpers.executionLabels()}"
}

WITH source_sessions_aggregation AS (

  SELECT
    s.session_date,
    s.session_id,
    IF(s.ga_session_number = 1, 'New Session', 'Returning Session') AS session_type,
    s.session_info.is_engaged_session,
    s.session_info.is_direct_session,
    s.device.category AS device_category,
    s.geo.country,
    s.platform,
    s.stream_id,

    s.last_non_direct_traffic_source.campaign AS session_campaign,
    s.last_non_direct_traffic_source.source AS session_source,
    s.last_non_direct_traffic_source.medium AS session_medium,
    s.last_non_direct_traffic_source.content AS session_content,
    s.last_non_direct_traffic_source.term AS session_term,
    s.last_non_direct_traffic_source.default_channel_grouping AS session_default_channel_grouping,

    s.session_traffic_source_last_click.google_ads_campaign.campaign_name AS last_click_google_ads_campaign,
    s.session_traffic_source_last_click.manual_campaign.campaign_name AS last_click_campaign_name,
    s.session_traffic_source_last_click.manual_campaign.source AS last_click_source,
    s.session_traffic_source_last_click.manual_campaign.medium AS last_click_medium,
    s.session_traffic_source_last_click.manual_campaign.term AS last_click_term,
    s.session_traffic_source_last_click.manual_campaign.content AS last_click_content,

    s.landing_page.landing_page_path,
    s.landing_page.landing_content_group,
    s.exit_page.exit_page_path,
    e.ecommerce.transaction_id,

    COUNTIF(e.event_name = 'page_view') AS page_view,
    COUNTIF(e.event_name = 'view_item') AS view_item,
    COUNTIF(e.event_name = 'add_to_cart') AS add_to_cart,
    COUNTIF(e.event_name = 'begin_checkout') AS begin_checkout,
    COUNTIF(e.event_name = 'purchase') AS purchase,
    COUNTIF(e.event_name = 'view_search_results') AS view_search_results,
    SUM(e.ecommerce.purchase_revenue) AS revenue

  FROM ${ref("ga4_sessions")} AS s
  LEFT JOIN ${ref("ga4_events")} AS e
    ON
      s.session_id = e.session_id
      AND e.event_name IN (
        'page_view',
        'view_item',
        'add_to_cart',
        'begin_checkout',
        'purchase',
        'view_search_results'
      )
  GROUP BY ALL

),

final_daily_aggregation AS (

  SELECT
    session_date,
    session_type,
    is_direct_session,
    device_category,
    country,
    platform,
    stream_id,

    -- "our" attribution
    COALESCE(session_campaign, '(not set)') AS session_campaign,
    COALESCE(session_source, '(not set)') AS session_source,
    COALESCE(session_medium, '(not set)') AS session_medium,
    COALESCE(session_content, '(not set)') AS session_content,
    COALESCE(session_term, '(not set)') AS session_term,
    session_default_channel_grouping,

    -- google attribution (if available)
    last_click_google_ads_campaign
      AS last_click_campaign_name,
    last_click_source,
    last_click_medium,
    last_click_term,
    last_click_content,

    landing_page_path,
    landing_content_group,
    exit_page_path,

    COUNT(DISTINCT session_id) AS sessions,
    COUNT(DISTINCT IF(is_engaged_session, session_id, NULL)) AS engaged_sessions,

    SUM(revenue) AS revenue,
    SUM(page_view) AS page_view,
    SUM(view_item) AS view_item,
    SUM(add_to_cart) AS add_to_cart,
    SUM(begin_checkout) AS begin_checkout,
    SUM(purchase) AS purchase,
    SUM(view_search_results) AS view_search_results,

    COUNT(DISTINCT IF(view_item > 0, session_id, NULL)) AS view_item_sessions,
    COUNT(DISTINCT IF(add_to_cart > 0, session_id, NULL)) AS add_to_cart_sessions,
    COUNT(DISTINCT IF(begin_checkout > 0, session_id, NULL)) AS begin_checkout_sessions,
    COUNT(DISTINCT IF(purchase > 0, session_id, NULL)) AS purchase_sessions,
    COUNT(DISTINCT transaction_id) AS transactions

  FROM source_sessions_aggregation
  GROUP BY ALL
)

SELECT *
FROM final_daily_aggregation
